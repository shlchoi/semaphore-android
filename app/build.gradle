apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'

def keystorePropertiesFile = rootProject.file("keystore.properties");
def keystoreProperties = new Properties()

android {
    if (keystorePropertiesFile.canRead()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        if (keystoreProperties != null) {
            signingConfigs {
                release {
                    storeFile file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                }
            }
        }
    } else {
        signingConfigs {
            release { }
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "ca.semaphore.app"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystoreProperties != null) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix ".debug"
        }
        beta {
            applicationIdSuffix ".beta"
        }
    }
    lintOptions {
        lintConfig file("lint.xml")
        abortOnError true
        ignoreWarnings false
        htmlReport true
        xmlReport true

        if (System.getenv("CIRCLECI") == "true") {
            xmlOutput file(System.getenv('CIRCLE_ARTIFACTS') + "/lint-results.xml")
            htmlOutput file(System.getenv('CIRCLE_ARTIFACTS') + "/lint-results.html")
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.google.firebase:firebase-core:9.2.1'
    compile 'com.google.firebase:firebase-auth:9.2.1'
    compile 'com.google.firebase:firebase-database:9.2.1'

    compile 'net.cachapa.expandablelayout:expandablelayout:2.7'

    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.facebook.stetho:stetho:1.4.2'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    testCompile 'junit:junit:4.12'
}

checkstyle {
    toolVersion = '6.7'
}

task checkstyle(type: Checkstyle) {
    configFile file('checkstyle.xml')
    source 'src/main/java'
    include '**/*.java'
    classpath = files()
    if (System.getenv("CIRCLECI") == "true") {
        reports {
            xml {
                destination System.getenv('CIRCLE_ARTIFACTS') + "/checkstyle-results.xml"
            }
            html {
                destination System.getenv('CIRCLE_ARTIFACTS') + "/checkstyle-results.html"
            }
        }
    }

}

apply plugin: 'com.google.gms.google-services'
